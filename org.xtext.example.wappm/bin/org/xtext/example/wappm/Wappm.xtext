grammar org.xtext.example.wappm.Wappm with org.eclipse.xtext.common.Terminals

generate wappm "http://www.xtext.org/example/wappm/Wappm"

WebModel :
  'webapp' name=ID '{'
    hypertext=HypertextLayer
    content=ContentLayer
  '}'
;

HypertextLayer :
  'hypertext' hyperName=ID '{'
     pages+=Page+
  '}'
;

Page : 
	StaticPage | DynamicPage
;

StaticPage :
	'static' name=ID
	'{'
		'path' path=URL
		('links' '{' links+=Link ( "," links+=Link)* '}' )?
	'}'
;

DynamicPage :
	IndexPage | DetailPage
;

DetailPage :
	'detail' name=ID 'uses' displayedClass=[WebClass|ID]
	'{'
		'path' path=URL
		('links' '{' links+=Link ( "," links+=Link)* '}' )?
	'}'
;

IndexPage :
	'index' name=ID 'uses' displayedClass=[WebClass|ID]
	'{'
		'path' path=URL
		('links' '{' links+=Link ( "," links+=Link)* '}' )?
	'}'
;

Link :
	'link' '{' 
		'page' page=[Page|ID]	
	'}'
;

ContentLayer :
  'content' contentName=ID '{'
    classes+=WebClass+
  '}' ;

WebClass :
  'class' name=ID '{' 
  	attributes+=Attribute+
  	'uniqueIdentifier' uniqueIdentifier=[Attribute|ID]
  '}' ;
  
Attribute : 
	'attr' name=ID ':' type=AppTypes
;
  
enum AppTypes :
  String | Integer | Float | Double | Boolean
;

URL :
	{URL} // object instantiation
	( '/' | ('/' (ID | addition+=URL_ADDITION))*)
;
 
URL_ADDITION :
	(':' name=ID | '?' name=ID)
;

