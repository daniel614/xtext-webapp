grammar org.xtext.example.wappm.Wappm with org.eclipse.xtext.common.Terminals

generate wappm "http://www.xtext.org/example/wappm/Wappm"

WebModel :
  'webapp' name=ID '{'
    hypertext=HypertextLayer
    content=ContentLayer
  '}'
;

HypertextLayer :
  'hypertext' hyperName=ID '{'
     pages+=Page+
  '}'
;

Page : 
	StaticPage | DynamicPage
;

StaticPage :
	'static' name=ID
	'{'
		'path' path=URL
		('links' '{' links+=Link ( "," links+=Link)* '}' )?
	'}'
;

DynamicPage :
	IndexPage | DetailPage
;

DetailPage :
	'detail' name=ID 'uses' displayedClass=[WebClass|ID]
	'{'
		'path' path=URL
		('links' '{' links+=Link ( "," links+=Link)* '}' )?
	'}'
;

IndexPage :
	'index' name=ID 'uses' displayedClass=[WebClass|ID]
	'{'
		'path' path=URL
		'size' size=INT
		('links' '{' links+=Link ( "," links+=Link)* '}' )?
	'}'
;

Link :
	'link' '{' 
		'page' page=[Page|ID]	
	'}'
;

ContentLayer :
  'content' contentName=ID '{'
    classes+=WebClass+
  '}' ;

WebClass :
  'class' name=ID '{' 
  	attributes+=Attribute+
  	references+=Reference*
  '}' ;
  
Attribute : 
	'attr' name=ID ':' type=AppTypes
;

Reference : 
	'ref' '{'
		'role' name=ID 
		'bound['lowBound=INT',' upBound=INT']'
	'}'
;
  
enum AppTypes :
  String | Integer | Float | Double | Boolean
;

URL :
	'/'(ID'/')*
;

